apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.kelvinievenes.popcorn"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "WS_BASE_URL", '"https://www.omdbapi.com/"'
        buildConfigField "String", "WS_API_KEY", '"764f1ac3"'
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/java'
            // List and include all res-* folders
            file('src/main/').listFiles().each {
                if (it.isDirectory() && it.name.contains('res')) {
                    res.srcDirs += it.path
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AppCompat
    implementation "androidx.appcompat:appcompat:$appCompatVersion"

    //Legacy Support
    implementation "androidx.legacy:legacy-support-v4:$legacySupportVersion"

    // KotlinX
    implementation "androidx.core:core-ktx:$kotlinXVersion"

    // Views
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "com.google.android:flexbox:$flexboxVersion"
    implementation "com.google.android.material:material:$materialDesignVersion"

    // Stetho
    implementation "com.facebook.stetho:stetho:$stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    // Koin for Android
    implementation "org.koin:koin-android:$koinVersion"

    // Glide (Image loader)
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // OkHttp, Retrofit and GSON
    implementation "com.squareup.retrofit2:converter-gson:$retrofitConverterGsonVersion"
    implementation("com.squareup.retrofit2:retrofit:$retrofitVersion") {
        // exclude Retrofitâ€™s OkHttp peer-dependency module and define your own module import
        exclude module: 'okhttp'
    }
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
}
